// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not


CHIP ALU {
    IN
        x[16], y[16],  // 16-bit inputs
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    Mux16(a=x, b=false, sel=zx, out=zerozx); // x = 0 - zx
    Not16(in=zerozx, out=notzx);
    Mux16(a=zerozx, b=notzx, sel=nx, out=xval); // bitwise not - nx

    Mux16(a=y, b=false, sel=zy, out=zerozy); // y = 0 - zy
    Not16(in=zerozy, out=notzy);
    Mux16(a=zerozy, b=notzy, sel=ny, out=yval); // bitwise not - ny

    Add16(a=xval, b=yval, out=add); // f = 1 2's compliment addition
    And16(a=xval, b=yval, out=and); // f = 0 bitwise and
    Mux16(a=and, b=add, sel=f, out=fout);

    Not16(in=fout, out=notout); // no = 1 bitwise not
    Mux16(a=fout, b=notout, sel=no, out=out1);

    Or16Way(in=out1, out=zrtmp); // zr - out is zero (1) else (0)
    Not(in=zrtmp, out=zr);

    // ng - if out < 0 ng=1 else ng=0
    Neg(in=out1, out=ng);
    Or16(a=out1, b=false, out=out);
}

